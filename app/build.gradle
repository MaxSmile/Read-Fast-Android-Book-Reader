apply plugin: 'com.android.application'

android {
    namespace = "com.vasilkoff.readfast"
    ndkVersion '16.1.4479499' // stripReleaseDebugSymbols
    compileSdkVersion 34
    defaultConfig {
        applicationId 'com.vasilkoff.readfast'
        minSdk 28
        targetSdk 34
        versionCode 1
        versionName '0.0.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    signingConfigs {
        release {
            storeFile file(project.findProperty('RELEASE_STORE_FILE') ?: 'none')
            storePassword project.findProperty('RELEASE_STORE_PASSWORD')
            keyAlias project.findProperty('RELEASE_KEY_ALIAS')
            keyPassword project.findProperty('RELEASE_KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + (output.getFilter(com.android.build.OutputFile.ABI) ?: "universal") + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    bundle {
        abi {
            enableSplit = true
        }
    }
}

configurations { // TODO fix library dependency copy
        resolvableImpl.extendsFrom(implementation)
        resolvableImpl.canBeResolved(true)
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.github.axet:android-library:1.35.21' // implementation project(':android-library')
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation ('com.github.axet.fbreader:library:0.1.18') { exclude group: 'org.apache.httpcomponents', module: 'httpclient' }
    implementation 'com.github.axet:djvulibre:0.1.4'
    implementation 'com.github.axet:pdfium:2.0.16'
    implementation 'com.github.axet:k2pdfopt:0.1.6'
    implementation ('com.github.axet:wget:1.7.0') { exclude group: 'org.json', module: 'json' }
}
